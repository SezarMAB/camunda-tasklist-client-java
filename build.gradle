plugins {
    id("com.apollographql.apollo3").version("3.2.1")
    id 'maven-publish'
    id 'java'
    id 'signing'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'org.camunda.community'
version = '0.0.13'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    def apolloVersion = "3.2.1"
    def httpClientVersion = "5.1.3"
    implementation "org.apache.httpcomponents.client5:httpclient5:${httpClientVersion}"
    implementation "org.apache.httpcomponents.client5:httpclient5-fluent:${httpClientVersion}"
    
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    
    implementation "com.apollographql.apollo3:apollo-runtime:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-api:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-ast:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-rx3-support:${apolloVersion}"
    
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}

tasks.named('test') {
    useJUnitPlatform()
}

apollo {
    generateKotlinModels.set(false)
    packageName.set("io.camunda.tasklist.client")
}
task sourceJar(type: Jar) {
    outputs.cacheIf { true }
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    outputs.cacheIf { true }
    from javadoc.destinationDir
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = 'javadoc'
}
java {
    withSourcesJar()
}

nexusPublishing {
  transitionCheckOptions {
    maxRetries = 100
    delayBetween = Duration.ofSeconds(5)
  }
  repositories {
    sonatype {
	  stagingProfileId = "13992b29744076"
	  nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
	  snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
	}
  }
}

signing {
    useInMemoryPgpKeys(System.getenv("ORG_GRADLE_PROJECT_signingKey"), System.getenv("ORG_GRADLE_PROJECT_signingPassword"))
    sign(publishing.publications)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      pom {
        groupId = 'org.camunda.community'
        artifactId = 'camunda-tasklist-client-java'
        name = 'Camunda 8 Tasklist Java Client'
        description = "Java client for the Tasklist API of Camunda Platform 8"
        url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        licenses {
          license {
           name = "The Apache License, Version 2.0"
           url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "christophe.dame"
            email = "christophe.dame@camunda.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          developerConnection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        }
      }
    }
  }
}

