plugins {
  id("com.apollographql.apollo3").version("3.2.1")
  id 'maven-publish'
  id 'java-library'
  id 'signing'
  id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
}

group = 'io.camunda'
version = '1.5.1'

sourceCompatibility = '8'

repositories {
  mavenCentral()
}

dependencies {
  def apolloVersion = "3.2.1"  
  api 'com.fasterxml.jackson.core:jackson-databind:2.13.1'

  api "com.apollographql.apollo3:apollo-runtime:${apolloVersion}"
  api "com.apollographql.apollo3:apollo-api:${apolloVersion}"
  api "com.apollographql.apollo3:apollo-ast:${apolloVersion}"
  api "com.apollographql.apollo3:apollo-rx3-support:${apolloVersion}"
    
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

tasks.named('test') {
  useJUnitPlatform()
}

apollo {
  generateKotlinModels.set(false)
  packageName.set("io.generated.tasklist.client")
}
task sourceJar(type: Jar) {
  outputs.cacheIf { true }
  from sourceSets.main.allJava
  archiveClassifier = 'sources'
}
javadoc {
  exclude '**/generated/**'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
  from javadoc.destinationDir
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  archiveClassifier = 'javadoc'
}
java {
  withSourcesJar()
  withJavadocJar()
}

nexusPublishing {
  transitionCheckOptions {
    maxRetries.set(100)
    delayBetween.set(Duration.ofSeconds(10))
  }
  repositories {
    sonatype {
	  stagingProfileId = "13992b29744076"
	  nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
	  snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
	}
  }
}

signing {
    useInMemoryPgpKeys(System.getenv("ORG_GRADLE_PROJECT_signingKey"), System.getenv("ORG_GRADLE_PROJECT_signingPassword"))
    sign(publishing.publications)
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      versionMapping {
         usage('java-api') {
             fromResolutionResult()
         }
      }
      pom {
        groupId = 'io.camunda'
        artifactId = 'camunda-tasklist-client-java'
        packaging = 'jar'
        name = 'Camunda 8 Tasklist Java Client'
        description = "Java client for the Tasklist API of Camunda Platform 8"
        url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        licenses {
          license {
           name = "The Apache License, Version 2.0"
           url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "christophe.dame"
            email = "christophe.dame@camunda.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          developerConnection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        }
      }
    }
  }
}

