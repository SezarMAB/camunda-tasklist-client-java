plugins {
    id("com.apollographql.apollo3").version("3.2.1")
    id("com.github.johnrengelman.shadow") version "7.0.0"
    id 'maven-publish'
    id 'java'
}

group = 'org.camunda.community'
version = '0.0.8-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
	def apolloVersion = "3.2.1"
	def httpClientVersion = "5.1.3"
    implementation "org.apache.httpcomponents.client5:httpclient5:${httpClientVersion}"
    implementation "org.apache.httpcomponents.client5:httpclient5-fluent:${httpClientVersion}"
	
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
	
    implementation "com.apollographql.apollo3:apollo-runtime:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-api:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-ast:${apolloVersion}"
    implementation "com.apollographql.apollo3:apollo-rx3-support:${apolloVersion}"
    
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

}

tasks.named('test') {
    useJUnitPlatform()
}

apollo {
    generateKotlinModels.set(false)
    packageName.set("io.camunda.tasklist.client")
}

shadowJar {
    archiveClassifier.set("")
}

publishing {
  publications {
    shadow(MavenPublication) { publication ->
      project.shadow.component(publication)
      
      artifactId = "camunda-tasklist-client-java"

      pom {
        name = "Camunda TaskList Client Java"
        description = "Java client for the Tasklist API of Camunda Platform 8"
        url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        licenses {
          license {
            name = "The Apache License, Version 2.0"
            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
          }
        }
        developers {
          developer {
            name = "christophe.dame"
            email = "christophe.dame@camunda.com"
          }
        }
        scm {
          connection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          developerConnection = "scm:git:git@github.com:camunda-community-hub/camunda-tasklist-client-java.git"
          url = "https://github.com/camunda-community-hub/camunda-tasklist-client-java"
        }
      }
    }
  }
  repositories {
    maven {
      url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
      credentials {
        username = System.getenv("USERNAME")
        password = System.getenv("TOKEN")
      }
    }
  }
}